<!DOCTYPE html>
<html>
<head>
<title>New design</title>
<link rel="stylesheet" type="text/css" href="new.css">
<meta charset="UTF-8"> 
</head>

<body>
<div class="topBar">
    <ul>
        <li><a href="/">HOME</a></li>
        <li><a href="/blog">BLOG</a></li>
        <li><a href="/cv">CV</a></li>
        <li><a href="/contact">CONTACT</a></li>
    </ul>
</div>
<div id="animatedHeader"></div>
<div class="content">
    <h1 class="center">THOMAS LEROY</h1>
</div>
<!--<input type="text" id="noiseScale1" value="0.0"/>
<input type="text" id="elevation1" value="0.0"/>
<input type="text" id="noiseScale2" value="0.0"/>
<input type="text" id="elevation2" value="0.0"/>
<input type="text" id="noiseScale3" value="0.0"/>
<input type="text" id="elevation3" value="0.0"/>
<input type="text" id="noiseScale4" value="0.0"/>
<input type="text" id="elevation4" value="0.0"/>-->
</body>

<div style="display:none" id="vertexShader">
uniform vec4 u_noiseScale;
uniform vec4 u_elevation;
uniform float u_time;
varying vec3 vert_pos;

vec3 permute(vec3 x) 
{
    return mod(((x*34.0)+1.0)*x, 289.0);
}

float snoise(vec2 v)
{
    const vec4 C = vec4(0.211324865405187, 0.366025403784439, -0.577350269189626, 0.024390243902439);
    vec2 i = floor(v + dot(v, C.yy) );
    vec2 x0 = v -   i + dot(i, C.xx);
    vec2 i1;
    i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
    vec4 x12 = x0.xyxy + C.xxzz;
    x12.xy -= i1;
    i = mod(i, 289.0);
    vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
    + i.x + vec3(0.0, i1.x, 1.0 ));
    vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy),
    dot(x12.zw,x12.zw)), 0.0);
    m = m*m ;
    m = m*m ;
    vec3 x = 2.0 * fract(p * C.www) - 1.0;
    vec3 h = abs(x) - 0.5;
    vec3 ox = floor(x + 0.5);
    vec3 a0 = x - ox;
    m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );
    vec3 g;
    g.x  = a0.x  * x0.x  + h.x  * x0.y;
    g.yz = a0.yz * x12.xz + h.yz * x12.yw;
    return 130.0 * dot(m, g);
}

void main()
{
    vec4 pos = modelMatrix * vec4(position, 1.0);
    float far = max(0.0, -pos.z) / 300.0;
    float y1 = snoise(vec2(pos.x, pos.z-u_time) * u_noiseScale.x) * (u_elevation.x * 1.0 + far * 10.0);
    float y2 = snoise(vec2(pos.x, pos.z-u_time) * u_noiseScale.y) * u_elevation.y;
    float y3 = snoise(vec2(pos.x, pos.z-u_time) * u_noiseScale.z) * u_elevation.z;
    float y4 = snoise(vec2(pos.x, pos.z-u_time) * u_noiseScale.w) * u_elevation.w;
    pos.y += y1 + y2 + y3 + y4;

    gl_Position = projectionMatrix * viewMatrix * pos;
    vert_pos = pos.xyz;
}
</div>

<div style="display:none" id="fragmentShader">
varying vec3 vert_pos;

void main()
{
    float f = min(abs(vert_pos.z), 300.0) / 300.0;
    gl_FragColor = vec4(vec3(pow(f, 6.0 * (1.0 - f*f))), 1.0);
}
</div>

<div style="display:none" id="vertPlanet">
varying vec3 vert_pos;
varying vec3 vert_normal;

void main()
{
    vec4 pos = modelMatrix * vec4(position, 1.0);
    gl_Position = projectionMatrix * viewMatrix * pos;
    vert_pos = pos.xyz;
    vert_normal = normal;
}
</div>

<div style="display:none" id="fragPlanet">
uniform vec3 u_color;

varying vec3 vert_pos;

void main()
{
    vec3 color = u_color;

    float a = 1.0 - (min(max(0.0, vert_pos.y - 100.0), 550.0) / 550.0);
    gl_FragColor = vec4(mix(color, vec3(1), a), 1.0);
}
</div>

<div style="display:none" id="fragShip">
uniform vec3 u_color;

varying vec3 vert_pos;
varying vec3 vert_normal;

void main()
{
    vec3 color = u_color;

    float a = min(abs(vert_pos.z), 300.0) / 300.0;
    float f = pow(a, 4.0 * (1.0 - a*a));
    // lighting
    float d = 0.7 + dot(normalize(vert_normal), normalize(-vec3(1, -1, 0.5))) * 0.3;
    gl_FragColor = vec4(mix(color * d, vec3(1), f), 1.0);
}
</div>

<div style="display:none" id="vertSmoke">
varying vec3 vert_pos;
varying vec2 vert_uv;

void main()
{
    vec4 pos = modelMatrix * vec4(position, 1.0);
    gl_Position = projectionMatrix * viewMatrix * pos;
    vert_pos = pos.xyz;
    vert_uv = uv;
}
</div>

<div style="display:none" id="fragSmoke">
uniform float u_time;
uniform sampler2D u_texture;

varying vec3 vert_pos;
varying vec2 vert_uv;

void main()
{
    const float frameCount = 30.0;
    const float frameW = 6.0;
    float frame = mod(floor(u_time * frameCount), frameCount);
    float x = mod(frame, frameW);
    float y = floor(frame / frameW);
    vec2 uv = (vert_uv + vec2(x, y)) / 6.0;
    vec3 color = texture2D(u_texture, uv).rgb;
    gl_FragColor = vec4(color * pow(vert_uv.y, 3.0), color.r);
}
</div>

<script src="jquery-3.3.1.min.js"></script>
<script src="three.min.js"></script>
<script src="OBJLoader.js"></script>
<script src="new.js"></script>
</html>